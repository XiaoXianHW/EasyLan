buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'

version = '1.5'
group = 'org.xiaoxian.easylan'
archivesBaseName = 'EasyLAN'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: '1.16.5'
    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.42'
    implementation('org.nanohttpd:nanohttpd:2.3.1')
}

configurations {
    customJarConfiguration {
        canBeResolved = true
        canBeConsumed = false
        extendsFrom implementation
    }
}

jar {
    manifest {
        attributes([
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    from {
        configurations.customJarConfiguration.filter {
            it.name.contains('nanohttpd')
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    eachFile { file ->
        if (file.name == 'LICENSE.txt') {
            file.exclude()
        }
    }
}

jar.finalizedBy('reobfJar')
